import flixel.FlxG;
import funkin.FunkinMemory;
import funkin.modding.events.ScriptEvent.SongLoadScriptEvent;
import funkin.modding.events.ScriptEvent;
import funkin.modding.module.ScriptedModule;
import funkin.play.notes.Strumline;
import funkin.play.PlayState;
import Lambda;

class TBSEventHandler extends ScriptedModule {
    var healthDrainChars:Array<String> = ["mouse 3", "mouse 3 dark", "mouse 3 rtx", "mouse angry ", "mouse angry 2"];

    var playState(get, never):PlayState;
    function get_playState():PlayState { return PlayState.instance; }

    var dodge(get, never):DodgeEvent;

    function get_dodge():DodgeEvent { return DodgeEvent.instance; }

    var hasDodgeEvent:Bool = false;

    override public function onSongLoaded(event:SongLoadScriptEvent) {
        if (playState == null) return;

        hasDodgeEvent = Lambda.exists(playState.currentChart.events, e -> e.eventKind == "DodgeEvent");

        if (hasDodgeEvent) {
            playState.currentStage.getDad().ignoreExclusionPref = [];
            playState.currentStage.getBoyfriend().ignoreExclusionPref = [];
            FunkinMemory.cacheTexture(Paths.image("spacebar"));
            FunkinMemory.cacheSound(Paths.sound("DODGE"));
            FunkinMemory.cacheSound(Paths.sound("Dodged"));
        }
    }

    override public function onUpdate(event:UpdateScriptEvent) {
        if (!dodge.canDodge) return;

        dodge.dodgeTimer -= event.elapsed;

        if ((FlxG.keys.justPressed.SPACE || playState.isBotPlayMode) && !dodge.dodged) {
            onDodged();
            return;
        }

        if (dodge.dodgeTimer <= 0 && !dodge.dodged) onDodgeFail();
    }

    function onDodged() {
        dodge.dodged = true;
        dodge.canDodge = false;

        playState.currentStage.getDad().playAnimation("attack", true, true);
        playState.currentStage.getBoyfriend().playAnimation("dodge", true, true);
        TBSMain.playSound("Dodged");

        playState.remove(dodge.spacebar);
    }

    function onDodgeFail() {
        dodge.canDodge = false;

        playState.currentStage.getDad().playAnimation("attack", true, true);
        playState.currentStage.getBoyfriend().playAnimation("singRIGHTmiss", true, true);
        TBSMain.playSound("dead", 0.7);
        playState.health -= 1.3;

        playState.remove(DodgeEvent.instance.spacebar);
    }

    var paused:Bool = false;
    override public function onPause(event:PauseScriptEvent) {
        if (dodge.canDodge) {
            paused = true;
            dodge.canDodge = false;
        }
    }

    override public function onResume(event:ScriptEvent) {
        if (paused) {
            dodge.canDodge = true;
            paused = false;
        }
    }

    override public function onNoteHit(event:HitNoteScriptEvent) {
        if (!healthDrainChars.contains(playState.currentStage.getDad().characterId)) return;
        if (playState.health <= 1) return;
        if (event.note.noteData.getStrumlineIndex(Strumline.KEY_COUNT) == 0) return;

        if (event.note.isHoldNote) playState.health -= (0.027 / 3);
        else playState.health -= 0.027;
    }

    override public function onSongEnd(event:ScriptEvent) {
        hasDodgeEvent = false;
        if (DodgeEvent.instance.spacebar != null) DodgeEvent.instance.spacebar.destroy();
        FunkinMemory.purgeSoundCache();
        FunkinMemory.purgeTextureCache();
    }
}